plugins {
	id 'java'
	id 'scala'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'io.freefair.lombok' version '8.6'
	id 'io.gatling.gradle' version '3.10.5'
}

group = 'org.aleajactarest'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_19
}

configurations {
	implementation.exclude module: "spring-boot-starter-logging"

	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.2")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.2")
	testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.5.2")
	gatling 'io.gatling:gatling-core:3.10.5'
	gatlingImplementation 'io.gatling.highcharts:gatling-charts-highcharts:3.10.5'
	implementation 'org.scala-lang:scala-library:2.13.13'
}

gatling {
	logLevel = 'DEBUG'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

springBoot {
	buildInfo()
}

tasks.register('printVersion') {
	doLast {
		println project.version
	}
}

tasks.named("bootBuildImage") {
	imageName = "${project.name}:latest"
	createdDate = "now"
	environment["BP_JVM_VERSION"] = "20"
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}

configure(unpack) {
	group = 'release'
	description = 'Unpacks the boot jar so we can build multi-layer images'
}
